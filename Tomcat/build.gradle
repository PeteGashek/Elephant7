// specify path to Tomcat root in gradle.properties as tomcatBase property
def webAppDir = "$tomcatBase/webapps"

def explodedWarDir = buildDir.path + "/exploded-war/" + rootProject.name

task buildExplodedWar(dependsOn: compileJava, type: Copy) {
    group = buildAndDeployTaskGroup

    into explodedWarDir

    doFirst {
        delete explodedWarDir
    }

    into("WEB-INF/classes") {
        from compileJava
    }
    into("WEB-INF/lib") {
        from configurations.runtime
    }
    into("resources") {
        from sourceSets.main.resources
    }
    into("META-INF") {
        from "MANIFEST.MF"
    }
}

task buildWar(dependsOn: buildExplodedWar, type: Zip) {
    group = buildAndDeployTaskGroup

    baseName = rootProject.name
    extension = "war"
    from explodedWarDir
}

def deployedExplodedWarPath = webAppDir + "/" + rootProject.name
def deployedWarPath = deployedExplodedWarPath + ".war"

task deployExplodedWar(dependsOn: buildExplodedWar, type: Copy) {
    group = buildAndDeployTaskGroup

    outputs.upToDateWhen { false }

    doFirst {
        delete deployedExplodedWarPath
        delete deployedWarPath
    }

    from buildDir.path + "/exploded-war"
    into webAppDir
}

task deployWar(dependsOn: buildWar, type: Copy) {
    group = buildAndDeployTaskGroup

    outputs.upToDateWhen { false }

    doFirst {
        delete deployedExplodedWarPath
    }

    from buildWar
    into webAppDir
}

buildJar.enabled = false

sourceSets.main.resources.srcDirs += coreResources.srcDirs

dependencies {
    compile project(":Core")

    compileOnly "javax.servlet:javax.servlet-api:$servletApiVersion"
}